# This is a more elaborate test for backtracing.

package require TinySoar;
set cycles {14 4}

sp {elaborate*top-state
    (state <s> ^superstate nil)
    -->
    (<s> ^ball <b1> ^ball <b2> ^ball <b3>)}

sp {elaborate*snc
    (state <s> ^impasse no-change ^attribute state)
    -->
    (<s> ^problem-space <p*s> ^count 0)
    (<p*s> ^name count)}

for {set count 0}  {$count < 3} {incr count 1} {
  sp "propose*count-$count
      (state <s> ^problem-space.name count ^superstate <ss>)
      (<ss> ^ball <b>)
      (<s> -^counted <b> ^count $count)
      -->
      (<s> ^operator <o> + =)
      (<o> ^name count ^object <b> ^count [expr $count + 1])"
}

sp {implement*count
    (state <s> ^problem-space.name count ^count <old*c> ^operator <o>)
    (<o> ^name count ^object <b> ^count { <new*c> <> <old*c> })
    -->
    (<s> ^counted <b> ^count <old*c> - ^count <new*c> +)}

sp {terminate*count
    (state <s> ^problem-space.name count ^count <c> ^operator <o>)
    (<o> ^name count ^count <c>)
    -->
    (<s> ^operator <o> @)}

sp {propose*return-result
    (state <s> ^problem-space.name count)
    -->
    (<s> ^operator <o> + <)
    (<o> ^name return-result)}

sp {implement*return-result
    (state <s> ^problem-space.name count ^operator.name return-result
               ^count <c> ^superstate <ss>)
    -->
    (<ss> ^count <c>)}

sp {terminate*return-result
    (state <s> ^problem-space.name count ^operator <o>)
    (<o> ^name return-result)
    -->
    (<s> ^operator <o> @)}

sp {propose*wait
    (state <s> ^superstate nil ^count <c>)
    -->
    (<s> ^operator <o> +)
    (<o> ^name wait)}

sp {terminate*wait
    (state <s> ^operator <o>)
    (<o> ^name wait)
    -->
    (<s> ^operator <o> @)}

sp {detect*success
    (state <s> ^superstate nil ^count <c> ^io.output-link <out>)
    -->
    (<out> ^success t)}

set post {string compare [print chunk-1] \
"chunk-1
(state <v1> ^ball <v2>)
(<v1> ^ball <v3>)
(<v1> ^ball <v4>)
-->
(<v1> ^count 3 +)"}