# What happens if two operators are indifferent to one another, and a
# third operator is better than one of the others? In Soar-8.2, we end
# up with an operator tie between the two unordered productions:
#
# (S2 ^attribute operator ^choices multiple
#       ^impasse tie ^item O1 ^item O3 ^quiescence t ^superstate S1 ^type state)
#

package require TinySoar;
set cycles 5

sp {propose*one*two*three
    (state <s> ^superstate nil)
    -->
    (<s> ^operator <o1> + ^operator <o2> + ^operator <o3> +)
    (<o1> ^name one)
    (<o2> ^name two)
    (<o3> ^name three)}

sp {propose*one*two*indifferent
    (state <s> ^operator <o1> + ^operator <o2> +)
    (<o1> ^name one)
    (<o2> ^name two)
    -->
    (<s> ^operator <o1> = <o2>)}

sp {propose*three*better*two
    (state <s> ^operator <o2> + ^operator <o3> +)
    (<o2> ^name two)
    (<o3> ^name three)
    -->
    (<s> ^operator <o3> > <o2>)}

sp {detect*success
    (state <s> ^superstate <ss> ^choices multiple ^impasse tie
               ^item <o1> -^item <o2> ^item <o3> ^quiescence t ^type state)

    (<ss> ^io.output-link <out>)
    (<o1> ^name one)
    (<o2> ^name two)
    (<o3> ^name three)
    -->
    (<out> ^success t)}
