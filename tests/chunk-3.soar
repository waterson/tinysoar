# This test checks to see if we can trace through an operator
# instantiation. It also tests to check if we can trace a negative
# condition, and builds a pretty useless chunk!

package require TinySoar
set cycles 9

sp {elaborate*top-state
    (state <s> ^superstate nil)
    -->
    (<s> ^ball <b>)
    (<b> ^color blue)}

# Here's a little decoration on the substate to verify that we safely
# clean up when popping goals.
sp {elaborate*snc
    (state <s> ^impasse no-change ^attribute state)
    -->
    (<s> ^foo bar)}

# This is the first operator. It will copy the ball to the substate,
# and should provide us with the condition for the chunk.
sp {propose*copy-ball
    (state <s> ^impasse no-change ^attribute state ^superstate <ss>)
    (<ss> ^ball <b>)
    (<b> ^color blue)
    (<s> -^thing <b>)
    -->
    (<s> ^operator <o> +)
    (<o> ^name copy-ball ^ball <b>)}

sp {implement*copy-ball-as-thing
    (state <s> ^operator <o>)
    (<o> ^name copy-ball ^ball <b>)
    -->
    (<s> ^thing <b>)}

# This is the second operator, which returns the result based on
# preferences and wmes created by the first operator.
sp {propose*count
    (state <s> ^thing <t>)
    (<t> -^counted t)
    -->
    (<s> ^operator <o> +)
    (<o> ^name count ^thing <t>)}

sp {implement*count
    (state <s> ^operator <o>)
    (<o> ^name count ^thing <t>)
    -->
    (<t> ^counted t)}

sp {propose*wait
    (state <s> ^superstate nil ^ball.counted t -^operator)
    -->
    (<s> ^operator <o> +)
    (<o> ^name wait)}

sp {detect*success
    (state <s> ^ball.counted t ^io.output-link <out>)
    -->
    (<out> ^success t)}

set post {string compare [print chunk-1] \
"chunk-1
(state <v1> ^ball <v2>)
(<v2> -^counted t)
(<v2> ^color blue)
-->
(<v2> ^counted t +)"}