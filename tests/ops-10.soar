# What happens if two operators are indifferent to one another, and
# one of the indifferent operators is preferred to a third operator?
# In Soar-8.2, both indifferent operators are considered better than
# the third operator, so we end up choosing between them.

package require TinySoar;
set cycles 4

sp {propose*one*two*three
    (state <s> ^superstate nil)
    -->
    (<s> ^operator <o1> + ^operator <o2> + ^operator <o3> +)
    (<o1> ^name one)
    (<o2> ^name two)
    (<o3> ^name three)}

sp {propose*one*two*indifferent
    (state <s> ^operator <o1> + ^operator <o2> +)
    (<o1> ^name one)
    (<o2> ^name two)
    -->
    (<s> ^operator <o1> = <o2>)}

sp {propose*two*better*three
    (state <s> ^operator <o2> + ^operator <o3> +)
    (<o2> ^name two)
    (<o3> ^name three)
    -->
    (<s> ^operator <o2> > <o3>)}

sp {implement*one
    (state <s> ^operator.name one ^io.output-link <out>)
    -->
    (<out> ^success t)}

sp {implement*two
    (state <s> ^operator.name two ^io.output-link <out>)
    -->
    (<out> ^success t)}

