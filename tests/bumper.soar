package require TinySoar;

# Drive forward until we bump into something.
sp {propose*drive
    (state <s> ^superstate nil ^io <io>)
    (<io> ^input-link <in>)
    (<in> ^sensor-1 1023)
    -->
    (<s> ^operator <o> +)
    (<o> ^name drive)}

sp {implement*drive
    (state <s> ^operator <o> ^io <io>)
    (<o> ^name drive)
    (<io> ^output-link <out>)
    -->
    (<out> ^motor-a 100 ^motor-c 100)}

sp {drive*reconsider
    (state <s> ^operator <o>)
    (<o> ^name drive)
    -->
    (<s> ^operator <o> @)}

# Once we've found the line, just wait.
sp {propose*wait
    (state <s> ^superstate nil)
    -->
    (<s> ^operator <o> +)
    (<o> ^name wait)}

sp {implement*wait
    (state <s> ^operator <o> ^io <io>)
    (<o> ^name wait)
    (<io> ^output-link <out>)
    (<out> ^motor-a <a> ^motor-c <c>)
    -->
    (<out> ^motor-a <a> - ^motor-c <c> -)}

sp {reconsider*wait
    (state <s> ^operator <o>)
    (<o> ^name wait)
    -->
    (<s> ^operator <o> @)}

# Prefer driving to waiting!
# XXX there's a bug here if we drop the ^superstate nil condition.
sp {prefer*drive-to-wait
    (state <s> ^superstate nil ^operator <a> + ^operator <b> +)
    (<a> ^name drive)
    (<b> ^name wait)
    -->
    (<s> ^operator <a> > <b>)}