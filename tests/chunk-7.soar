# This tests a couple things. First, it determines whether or not we
# can backtrace through an attribute that's created by the
# architecture; namely, the `^item' attribute. Second, it tests
# whether or not we'll pick up the not-equal test when creating the
# chunk.

package require TinySoar;
set cycles {3 4}

sp {propose*one*two
    (state <s> ^superstate nil)
    -->
    (<s> ^operator <o1> + ^operator <o2> +)
    (<o1> ^name one)
    (<o2> ^name two)}

sp {implement*one
    (state <s> ^io.output-link <out> ^operator.name one)
    -->
    (<out> ^success t)}

sp {onc*prefer*one
    (state <s> ^item <o1> ^item { <o2> <> <o1> } ^superstate <ss>)
    (<o1> ^name one)
    (<o2> ^name two)
    -->
    (<ss> ^operator <o1> > <o2>)}

set post {string compare [print chunk-1] \
"chunk-1
(state <v1> ^operator <v2> +)
(<v1> ^operator { <v3> <> <v2> } +)
(<v2> ^name one)
(<v3> ^name two)
-->
(<v1> ^operator <v2> > <v3>)"}

